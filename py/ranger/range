#!/usr/local/bin/gawk -f
# set filetype=awk tabstop=8 softtabstop=0 expandtab shiftwidth=4 smarttab

BEGIN { srand(Seed ? Seed : 1) 
	FS   = ","  
	Best = 0.25 }
      { update( NR-1 )
        if (NR==1) { print $0 ",dom"; next }  }
END   { doms( NF+1 , 20/Best) 
	dump(Data) }

######### ######### ######### ######### ######### ######### 
function update(r,   c) { 
  for(c=1; c<=NF; c++) { 
    if (NR==1) 
       header(c,$c) 
    else {
       Data[r][c] = $c 
       if (($c != "?") && (c in Hi)) {
	 Hi[c] = $c > Hi[c] ? $c : Hi[c]
	 Lo[c] = $c < Lo[c] ? $c : Lo[c] }}}
}
func header(c,s) {
  if ( s ~ /</ ) W[c] = -1
  if ( s ~ />/ ) W[c] =  1
  if ( s ~ /[<>\$]/ ) { Hi[c]= -10^32; Lo[c]=  10^32 }
}
function dom1(r1,r2,   w,n,c,a,b,s1,s2) {
  n = length(W) + 10^-32
  for (c in W) {
    w   = W[c]
    a   = Data[r1][c]
    b   = Data[r2][c]
    a   = (a - Lo[c]) / (Hi[c] - Lo[c] + 10^-32)
    b   = (b - Lo[c]) / (Hi[c] - Lo[c] + 10^-32)
    s1 -= 10^(w * (a - b) / n)
    s2 -= 10^(w * (b - a) / n)  }
  return s1 / n < s2 / n
}
function dom(r,m,         i,n) {
  while(m-- > 0)
    n += dom1(r, anyBut(r,Data)) 
  return n 
}
function doms(k,m,      r) {
  for(r in Data)  
    Data[r][k] = dom(r,m)/m
}
######### ######### ######### ######### ######### ######### 
function anyBut(x,l,     y)  { 
  y = int(rand() * length(l)) + 1 
  return x==y ? anyBut(x,l) : y
}
function dump(l,  line,i,j,sep) {
  for(i in l)  {
    line = sep = ""
    for(j in l[i]) { line = line sep l[i][j]; sep  = "," }
    print line }
}
function rogues(    s) {
  for(s in SYMTAB) 
    if (s ~ /^[A-Z][a-z]/) print "Global " s
  for(s in SYMTAB) 
    if (s ~ /^[_a-z]/) print "Rogue: " s
}
